/datum/antagonist/vassal
	name = "\improper Vassal"
	roundend_category = "Vassal"
	antagpanel_category = "Vampire"
	show_in_roundend = FALSE
	hud_icon = 'modular_nova/modules/bloodsucker/icons/antag_hud.dmi'
	antag_hud_name = "vassal"
	stinger_sound = 'sound/effects/magic/mutate.ogg'
	hijack_speed = 0

	pref_flag = ROLE_VASSAL

	desensitized_modifier = DESENSITIZED_THRESHOLD

	/// The Master Vampire's antag datum.
	var/datum/antagonist/vampire/master
	/// The Vampire's team
	var/datum/team/vampire/vampire_team
	/// List of Powers, like Vampires.
	var/list/datum/action/powers = list()
	/// A link to our team monitor, used to track our master.
	var/atom/movable/screen/tracking_arrow/tracking_arrow

	/// How much time has been spent away from their master, used for moodlets.
	var/time_away_from_master = 0
	var/last_life_tick = 0

/datum/antagonist/vassal/antag_panel_data()
	return "Master : [master.owner.name]"

/datum/antagonist/vassal/apply_innate_effects(mob/living/mob_override)
	. = ..()
	var/mob/living/current_mob = mob_override || owner.current

	last_life_tick = world.time

	RegisterSignal(current_mob, COMSIG_ATOM_EXAMINE, PROC_REF(on_examine))
	RegisterSignals(current_mob, list(COMSIG_MOB_LOGIN, COMSIG_MOVABLE_Z_CHANGED), PROC_REF(on_login))
	RegisterSignal(current_mob, COMSIG_MOB_UPDATE_SIGHT, PROC_REF(on_update_sight))
	RegisterSignal(current_mob, COMSIG_MOVABLE_MOVED, PROC_REF(on_moved))
	RegisterSignal(current_mob, COMSIG_LIVING_LIFE, PROC_REF(on_life))

	current_mob.update_sight()

	// HUD
	add_team_hud(current_mob)

	// Tracking
	// setup_monitor(current_mob)
	current_mob.grant_language(/datum/language/vampiric, source = LANGUAGE_VASSAL)

	current_mob.add_faction(FACTION_VAMPIRE)

	current_mob.clear_mood_event("vampcandle")

	if(current_mob.hud_used)
		on_hud_created()
	else
		RegisterSignal(current_mob, COMSIG_MOB_HUD_CREATED, PROC_REF(on_hud_created))

/datum/antagonist/vassal/remove_innate_effects(mob/living/mob_override)
	. = ..()
	var/mob/living/current_mob = mob_override || owner.current

	UnregisterSignal(current_mob, list(
		COMSIG_ATOM_EXAMINE,
		COMSIG_MOB_LOGIN,
		COMSIG_MOVABLE_Z_CHANGED,
		COMSIG_MOB_UPDATE_SIGHT,
		COMSIG_MOB_HUD_CREATED,
		COMSIG_MOVABLE_MOVED,
		COMSIG_LIVING_LIFE,
	))
	current_mob.update_sight()
	current_mob.clear_mood_event("vassal")

	// Tracking
	remove_hud_elements(current_mob)
	current_mob.remove_language(/datum/language/vampiric, source = LANGUAGE_VASSAL)

	// Remove traits
	REMOVE_TRAITS_IN(current_mob, TRAIT_VAMPIRE)
	current_mob.remove_faction(FACTION_VAMPIRE)

/datum/antagonist/vassal/on_gain()
	. = ..()
	if(!master)
		owner.remove_antag_datum(src)
		CRASH("[owner.current] was vassilized without a master!")

	RegisterSignal(SSsol, COMSIG_SOL_WARNING_GIVEN, PROC_REF(give_warning))

	ADD_TRAIT(owner, TRAIT_VAMPIRE_ALIGNED, REF(src))

	vampire_team = master.vampire_team
	vampire_team.add_member(owner)

	// Enslave them to their Master
	master.vassals |= src
	owner.enslave_mind_to_creator(master.owner.current)
	owner.current.log_message("has been vassalized by [master.owner.name]!", LOG_ATTACK, color="#960000")

	// Give powers
	grant_power(new /datum/action/cooldown/vampire/recuperate)
	grant_power(new /datum/action/cooldown/vampire/distress)

	// Give objectives
	forge_objectives()

/datum/antagonist/vassal/on_removal()
	UnregisterSignal(SSsol, COMSIG_SOL_WARNING_GIVEN)
	REMOVE_TRAIT(owner, TRAIT_VAMPIRE_ALIGNED, REF(src))

	// Free them from their Master
	if(master)
		master.vassals -= src
		owner.enslaved_to = null

	vampire_team.remove_member(owner)
	vampire_team = null

	// Remove powers
	for(var/datum/action/cooldown/vampire/power in powers)
		powers -= power
		power.Remove(owner.current)

	return ..()

/datum/antagonist/vassal/on_body_transfer(mob/living/old_body, mob/living/new_body)
	. = ..()
	for(var/datum/action/cooldown/vampire/power in powers)
		power.Remove(old_body)
		power.Grant(new_body)

/*
/datum/antagonist/vassal/after_body_transfer(mob/living/old_body, mob/living/new_body)
	addtimer(CALLBACK(src, TYPE_PROC_REF(/datum/antagonist, add_team_hud), new_body), 0.5 SECONDS, TIMER_OVERRIDE | TIMER_UNIQUE) //i don't trust this to not act weird
*/

/datum/antagonist/vassal/greet()
	var/mob/living/living_vassal = owner.current
	var/mob/living/living_master = master.owner.current

	// Alert vassal
	var/list/msg = list()
	msg += span_cult_large("You are now the mortal servant of [master.owner.name], a Vampire!")
	msg += span_cult("You are not required to obey any other Vampire, for only [master.owner.name] is your master. The laws of Nanotrasen do not apply to you now; only your Master's word must be obeyed.")
	to_chat(living_vassal, boxed_message(msg.Join("\n")))

	play_stinger()

	antag_memory += "You are the mortal servant of <b>[master.owner.name]</b>, a vampire!<br>"

	// Alert master
	to_chat(living_master, span_userdanger("[living_vassal] has become addicted to your immortal blood. [living_vassal.p_They()] [living_vassal.p_are()] now your undying servant"))
	living_master.playsound_local(null, 'sound/effects/magic/mutate.ogg', 100, FALSE, pressure_affected = FALSE)

/datum/antagonist/vassal/farewell()
	if(silent)
		return

	owner.current.visible_message(
		span_deconversion_message("[owner.current]'s eyes dart feverishly from side to side, and then stop. [owner.current.p_They()] seem[owner.current.p_s()] calm, \
			like [owner.current.p_they()] [owner.current.p_have()] regained some lost part of [owner.current.p_them()]self."),
		span_deconversion_message("With a snap, you are no longer enslaved to [master.owner]! You breathe in heavily, having regained your free will, albeit the memories of your time serving them feel like a vague fever dream...")
	)
	owner.current.playsound_local(null, 'sound/effects/magic/mutate.ogg', 100, FALSE, pressure_affected = FALSE)

	// Alert master
	if(master.owner)
		to_chat(master.owner, span_cult_bold("You feel the bond with your vassal [owner.name] has somehow been broken!"))

/datum/antagonist/vassal/on_mindshield(mob/implanter, mob/living/mob_override)
	var/mob/living/target = mob_override || owner.current
	target.log_message("has been deconverted from Vassalization by [key_name(implanter)]!", LOG_ATTACK, color="#960000")
	owner.remove_antag_datum(/datum/antagonist/vassal)
	return COMPONENT_MINDSHIELD_DECONVERTED

/datum/antagonist/vassal/proc/on_login()
	SIGNAL_HANDLER
	var/mob/living/current = owner.current
	if(!QDELETED(current))
		addtimer(CALLBACK(src, TYPE_PROC_REF(/datum/antagonist, add_team_hud), current), 0.5 SECONDS, TIMER_OVERRIDE | TIMER_UNIQUE) //i don't trust this to not act weird

/datum/antagonist/vassal/admin_add(datum/mind/new_owner, mob/admin)
	var/list/datum/mind/possible_vampires = list()

	// Get possible vampires
	for(var/datum/antagonist/vampire/vampire in GLOB.antagonists)
		var/datum/mind/vampire_mind = vampire.owner
		if(QDELETED(vampire_mind?.current) || vampire_mind.current.stat == DEAD)
			continue

		possible_vampires += vampire_mind

	if(!length(possible_vampires))
		return

	// CHOOSE A DAMN PERSON
	var/datum/mind/choice = tgui_input_list(admin, "Which vampire should this vassal belong to?", "Vampire", possible_vampires)
	if(!choice)
		return

	log_admin("[key_name_admin(usr)] turned [key_name_admin(new_owner)] into a vassal of [key_name_admin(choice)]!")
	var/datum/antagonist/vampire/vampire = IS_VAMPIRE(choice.current)
	master = vampire
	new_owner.add_antag_datum(src)

	to_chat(choice, span_notice("Through divine intervention, you've gained a new vassal!"))

/datum/antagonist/vassal/forge_objectives()
	var/datum/objective/vampire/vassal/vassal_objective = new
	vassal_objective.owner = owner
	objectives += vassal_objective
	addtimer(CALLBACK(src, TYPE_PROC_REF(/datum, update_static_data_for_all_viewers)), 0.5 SECONDS, TIMER_UNIQUE | TIMER_OVERRIDE)

/datum/antagonist/vassal/add_team_hud(mob/target)
	QDEL_NULL(team_hud_ref)

	var/datum/atom_hud/alternate_appearance/basic/has_antagonist/hud = target.add_alt_appearance(
		/datum/atom_hud/alternate_appearance/basic/has_antagonist,
		"antag_team_hud_[REF(src)]",
		hud_image_on(target),
	)
	team_hud_ref = WEAKREF(hud)

	var/list/mob/living/mob_list = list()
	for(var/datum/antagonist/antag as anything in GLOB.antagonists)
		if(!istype(antag, /datum/antagonist/vampire) && !istype(antag, /datum/antagonist/vassal))
			continue
		var/mob/living/current = antag.owner?.current
		if(!QDELETED(current))
			mob_list |= current

	for (var/datum/atom_hud/alternate_appearance/basic/has_antagonist/antag_hud as anything in GLOB.has_antagonist_huds)
		if(!(antag_hud.target in mob_list))
			continue
		antag_hud.show_to(target)
		hud.show_to(antag_hud.target)

/* /datum/antagonist/vassal/proc/setup_monitor(mob/target)
	QDEL_NULL(monitor)
	if(QDELETED(master?.owner?.current) || QDELETED(master.tracker))
		return

	monitor = target.AddComponent(/datum/component/team_monitor, REF(master))
	monitor.add_to_tracking_network(master.tracker.tracking_beacon)
	monitor.show_hud(target) */

/datum/antagonist/vassal/proc/on_examine(datum/source, mob/examiner, list/examine_text)
	SIGNAL_HANDLER

	var/text = "<img class='icon' src='\ref['modular_nova/modules/bloodsucker/icons/vampiric.dmi']?state=vassal'> "

	var/datum/antagonist/vampire/vampiredatum = IS_VAMPIRE(examiner)
	if(src in vampiredatum?.vassals)
		text += span_cult("<EM>This is your vassal!</EM>")
		examine_text += text
	else if(vampiredatum || (master?.broke_masquerade && IS_CURATOR(examiner)) || IS_VASSAL(examiner))
		text += span_cult("<EM>This is [master.return_full_name()]'s vassal</EM>")
		examine_text += text

// Vassals get night vision, so they can at least somewhat be useful to their masters in dark areas without revealing themselves with a flashlight or something.
// The night vision is weaker than true night vision like a vampire has, but it's still better than mesons.
/datum/antagonist/vassal/proc/on_update_sight(mob/user)
	SIGNAL_HANDLER
	user.lighting_cutoff = max(user.lighting_cutoff, round((LIGHTING_CUTOFF_HIGH + LIGHTING_CUTOFF_MEDIUM) / 2, 1))
	user.lighting_color_cutoffs = user.lighting_color_cutoffs ? blend_cutoff_colors(user.lighting_color_cutoffs, list(25, 8, 5)) : list(25, 8, 5)

/// Used when your Master teaches you a new Power.
/datum/antagonist/vassal/proc/grant_power(datum/action/cooldown/vampire/power)
	powers += power
	power.Grant(owner.current)
	log_uplink("[key_name(owner.current)] has received \"[power]\" as a vassal")

/datum/antagonist/vassal/proc/on_hud_created(datum/source)
	SIGNAL_HANDLER
	var/datum/hud/hud_used = owner.current.hud_used

	tracking_arrow = new /atom/movable/screen/tracking_arrow(null, hud_used)
	hud_used.static_inventory |= tracking_arrow

	hud_used.show_hud(hud_used.hud_version)
	UnregisterSignal(owner.current, COMSIG_MOB_HUD_CREATED)

	var/mob/living/master_body = master?.owner?.current
	if(!QDELETED(master_body))
		tracking_arrow.update(owner.current, master_body)

/datum/antagonist/vassal/proc/remove_hud_elements(mob/living/current_mob)
	var/datum/hud/hud_used = current_mob?.hud_used
	if(hud_used)
		hud_used.static_inventory -= tracking_arrow
		hud_used.show_hud(hud_used.hud_version)
	QDEL_NULL(tracking_arrow)

/datum/antagonist/vassal/proc/on_moved()
	SIGNAL_HANDLER
	if(!tracking_arrow)
		return
	var/mob/living/our_mob = owner.current
	var/mob/living/master_mob = master?.owner?.current
	if(QDELETED(our_mob) || QDELETED(master_mob))
		tracking_arrow.invisibility = INVISIBILITY_ABSTRACT
		return
	tracking_arrow.update(our_mob, master_mob)

/datum/antagonist/vassal/proc/on_life(datum/source)
	SIGNAL_HANDLER
	var/mob/living/current = owner.current
	if(QDELETED(current) || current.stat != CONSCIOUS)
		return
	var/mob/living/master_body = master.owner.current
	if(QDELETED(master_body))
		return
	time_away_from_master += (world.time - last_life_tick)
	last_life_tick = world.time
	if(CAN_THEY_SEE(master_body, current))
		time_away_from_master = 0
		current.add_mood_event("vassal", /datum/mood_event/vassal_happy)
	else if(time_away_from_master >= 25 MINUTES)
		current.add_mood_event("vassal", /datum/mood_event/vassal_away_severe)
		current.set_jitter_if_lower(5 SECONDS)
	else if(time_away_from_master >= 5 MINUTES)
		current.add_mood_event("vassal", /datum/mood_event/vassal_away)
	else
		current.clear_mood_event("vassal")

/datum/antagonist/vassal/proc/give_warning(atom/source, danger_level, vampire_warning_message, vassal_warning_message)
	SIGNAL_HANDLER

	if(!owner?.current)
		return
	to_chat(owner, vassal_warning_message || vampire_warning_message, type = MESSAGE_TYPE_WARNING)

	switch(danger_level)
		if(DANGER_LEVEL_FIRST_WARNING)
			owner.current.playsound_local(null, 'modular_nova/modules/bloodsucker/sound/griffin_3.ogg', 50, TRUE)
		if(DANGER_LEVEL_SECOND_WARNING)
			owner.current.playsound_local(null, 'modular_nova/modules/bloodsucker/sound/griffin_5.ogg', 50, TRUE)
		if(DANGER_LEVEL_THIRD_WARNING)
			owner.current.playsound_local(null, 'sound/effects/alert.ogg', 75, TRUE)
		if(DANGER_LEVEL_SOL_ROSE)
			owner.current.playsound_local(null, 'sound/ambience/misc/ambimystery.ogg', 75, TRUE)
		if(DANGER_LEVEL_SOL_ENDED)
			owner.current.playsound_local(null, 'sound/music/antag/bloodcult/ghosty_wind.ogg', 90, TRUE)
