// Slime vampires have some unique behavior, so I'm just gonna stick it all in this one file.

/// Handles setting up self-revival when an slime vampire dies.
/datum/antagonist/vampire/proc/on_slime_core_ejected(datum/source, obj/item/organ/brain/slime/core)
	SIGNAL_HANDLER
	if(QDELETED(core) || final_death)
		return
	core.organ_flags |= ORGAN_FROZEN
	if(current_vitae < SLIME_MIN_REVIVE_BLOOD_THRESHOLD)
		to_chat(core.brainmob, span_narsiesmall("You do not have enough vitae to recollect yourself on your own!"), type = MESSAGE_TYPE_WARNING)
		return
	adjust_blood_volume(-SLIME_MIN_REVIVE_BLOOD_THRESHOLD * 0.5)
	to_chat(core.brainmob, span_narsiesmall("You begin recollecting yourself. You will rise again soon, if your core remains undisturbed."), type = MESSAGE_TYPE_INFO)
	new /datum/vampire_slime_reviver(src, core)

/// Heals an slime vampire's organs when they revive.
/datum/antagonist/vampire/proc/on_slime_revive(datum/source, mob/living/carbon/human/new_body, obj/item/organ/brain/slime/core)
	SIGNAL_HANDLER
	core.organ_flags &= ~ORGAN_FROZEN
	if(ishuman(owner.current))
		var/mob/living/carbon/human/human_owner = owner.current
		human_owner.regenerate_limbs()
	heal_vampire_organs()

/datum/antagonist/vampire/proc/slime_self_revive(obj/item/organ/brain/slime/core)
	if(QDELETED(core) || final_death)
		return
#ifdef is_slime_core
	var/mob/living/carbon/human/new_body = core.rebuild_body(nugget = FALSE)
	to_chat(new_body, span_cult_large("You recollect yourself, your vitae reforming your body from your core!"), type = MESSAGE_TYPE_INFO)
#else
	core.regenerate()
	to_chat(owner.current, span_cult_large("You recollect yourself, your vitae reforming your body from your core!"), type = MESSAGE_TYPE_INFO)
#endif

/// Stupid datum used for, to avoid a bunch of slime-specific vars on the vampire datum
/datum/vampire_slime_reviver
	/// The parent vampire antag datum.
	var/datum/antagonist/vampire/vampire
	/// The slime core we're reviving.
	var/obj/item/organ/brain/slime/core
	/// Progress ticker used for reviving.
	var/slime_revival_progress = SLIME_VAMPIRE_REVIVE_TIME
	/// The last world.time where we processed.
	var/last_process
	/// Cooldown for sending a chat message to the slime player how long until they revive.
	COOLDOWN_DECLARE(reminder_cooldown)

/datum/vampire_slime_reviver/New(datum/antagonist/vampire/vampire, obj/item/organ/brain/slime/core)
	src.vampire = vampire
	src.core = core
	src.last_process = world.time
	RegisterSignal(core, COMSIG_QDELETING, PROC_REF(delete_self))
	RegisterSignal(vampire.owner, COMSIG_SLIME_REVIVED, PROC_REF(delete_self))
	START_PROCESSING(SSfastprocess, src)

/datum/vampire_slime_reviver/Destroy(force)
	UnregisterSignal(core, COMSIG_QDELETING)
	UnregisterSignal(vampire.owner, COMSIG_SLIME_REVIVED)
	STOP_PROCESSING(SSfastprocess, src)
	vampire = null
	core = null
	return ..()

/datum/vampire_slime_reviver/proc/delete_self()
	SIGNAL_HANDLER
	if(!QDELETED(src))
		qdel(src)

/datum/vampire_slime_reviver/proc/progress_multiplier()
	. = 1
	if(HAS_TRAIT(core, TRAIT_BEINGSTAKED))
		return 0
	if(istype(core.loc, /obj/structure/closet/crate/coffin))
		return SLIME_VAMPIRE_REVIVE_COFFIN_MULTIPLIER
	if(SSsol.sunlight_active) // if we're not in a coffin, we won't regen during Sol
		return 0
	var/mob/living/holder = get(core, /mob/living)
	if(!QDELETED(holder))
		if(HAS_MIND_TRAIT(holder, TRAIT_VAMPIRE_ALIGNED))
			return SLIME_VAMPIRE_REVIVE_ALLY_MULTIPLIER
		else
			return SLIME_VAMPIRE_REVIVE_HELD_MULTIPLIER

/datum/vampire_slime_reviver/process(seconds_per_tick)
	if(QDELETED(core) || !core.core_ejected || vampire.final_death)
		delete_self()
		return
	slime_revival_progress -= (world.time - last_process) * progress_multiplier()
	last_process = world.time
	if(slime_revival_progress <= 0)
		vampire.slime_self_revive(core)
		delete_self()
	else if(COOLDOWN_FINISHED(src, reminder_cooldown))
		var/progress = round((1 - (slime_revival_progress / SLIME_VAMPIRE_REVIVE_TIME)) * 100, 1)
		to_chat(core.brainmob, span_cult_large("Your vitae coagulates... You are approximately [progress]% reformed."), type = MESSAGE_TYPE_INFO)
		core.balloon_alert(core.brainmob, "[progress]% reformed...")
		COOLDOWN_START(src, reminder_cooldown, 15 SECONDS)
