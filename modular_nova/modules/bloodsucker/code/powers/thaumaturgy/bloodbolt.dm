/datum/action/cooldown/vampire/targeted/bloodbolt
	name = "Thaumaturgy: Blood Bolt"
	desc = "Fire a blood bolt at your enemy, dealing Burn damage."
	button_icon_state = "power_bloodbolt"
	active_background_icon_state = "tremere_power_plat_on"
	base_background_icon_state = "tremere_power_plat_off"
	power_explanation = "Shoots a blood bolt spell that deals burn damage"
	vampire_power_flags = NONE
	vampire_check_flags = BP_CANT_USE_IN_TORPOR | BP_CANT_USE_WHILE_STAKED | BP_CANT_USE_IN_FRENZY | BP_CANT_USE_WHILE_INCAPACITATED | BP_CANT_USE_WHILE_UNCONSCIOUS
	vitaecost = 75
	cooldown_time = 60 SECONDS
	target_range = 80 // Sniper :)
	power_activates_immediately = FALSE
	prefire_message = "Select your target."
	ranged_mousepointer = 'modular_nova/modules/bloodsucker/icons/mouse_pointers/vampire_bloodbolt.dmi'

/datum/action/cooldown/vampire/targeted/bloodbolt/fire_targeted_power(atom/target_atom)
	. = ..()
	var/mob/living/living_owner = owner
	// check_witnesses(target_atom)
	living_owner.balloon_alert(living_owner, "you fire a blood bolt!")
	living_owner.face_atom(target_atom)
	living_owner.changeNext_move(CLICK_CD_RANGE)
	living_owner.newtonian_move(get_dir(target_atom, living_owner))

	var/obj/projectile/magic/arcane_barrage/vampire/bolt = new(living_owner.loc)
	// bolt.vampire_power = src
	bolt.firer = living_owner
	bolt.fired_from = src
	bolt.original = target_atom
	bolt.def_zone = ran_zone(living_owner.zone_selected)
	bolt.aim_projectile(target_atom, living_owner)
	INVOKE_ASYNC(bolt, TYPE_PROC_REF(/obj/projectile, fire))

	playsound(living_owner, 'modular_nova/modules/bloodsucker/sound/bloodbolt_fire.ogg', 60, TRUE)
	power_activated_sucessfully()

/**
 * 	# Blood Bolt
 *
 *	This is the projectile this Power will fire.
 */
/obj/projectile/magic/arcane_barrage/vampire
	name = "blood bolt"
	icon_state = "mini_leaper"
	damage = 40
	hitsound = 'modular_nova/modules/bloodsucker/sound/bloodbolt.ogg'
	antimagic_flags = MAGIC_RESISTANCE_HOLY
	// var/datum/action/cooldown/vampire/targeted/bloodbolt/vampire_power

/obj/projectile/magic/arcane_barrage/vampire/on_hit(atom/target, blocked = 0, pierce_hit)
	if(istype(target, /obj/structure/closet))
		var/obj/structure/closet/hit_closet = target
		hit_closet.bust_open()
		qdel(src)
		return BULLET_ACT_HIT

	if(istype(target, /obj/machinery/door/airlock) || istype(target, /obj/machinery/door/window))
		var/obj/machinery/door/airlock = target
		airlock.open(FORCING_DOOR_CHECKS)
		qdel(src)
		return BULLET_ACT_HIT

	if(isliving(target))
		var/mob/living/living_target = target
		living_target.adjust_blood_volume(-50)
		living_target.emote("scream")
		living_target.set_jitter_if_lower(6 SECONDS)
		living_target.Unconscious(3 SECONDS)
		visible_message(span_danger("[living_target]'s wounds spray boiling hot blood!"), span_userdanger("Oh god it burns!"))
		qdel(src)
		return BULLET_ACT_HIT
	. = ..()
